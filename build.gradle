buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
    }
}

allprojects {
    apply plugin: "eclipse"

    version = '1.0'
    ext {
        appName = "ChainedClimber2D"
        gdxVersion = '1.12.1'
        javaVersion = '17'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }
}

project(":desktop") {
    apply plugin: "java-library"

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":android") {
    apply plugin: "com.android.application"

    configurations { natives }

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    }
}

project(":core") {
    apply plugin: "java-library"

    sourceSets {
        main {
            java.srcDirs = [ "src/" ]
        }
        test {
            java.srcDirs = [ "test/" ]
        }
    }

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        
        // Testing dependencies
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:5.10.0"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.0"
        testImplementation "org.mockito:mockito-core:5.5.0"
        testImplementation "org.mockito:mockito-junit-jupiter:5.5.0"
        testImplementation "org.assertj:assertj-core:3.24.2"
        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
    
    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
            showStandardStreams = false
        }
        reports {
            html.required = true
            junitXml.required = true
        }
    }
}
